@input PlayerName summary:"Как звать твоего героя?"
@stop

@set AylaName="Айла"
@set DravenName="Дрейвен"

@set PlayerHasQuest=false
@set PlayerHasItem=false
@set Location3Unlocked=false
@set MinigameAccepted=false
@set MinigameCompleted=false
@set ItemFound=false

; ==============================================================================
# Map

@printer wait:false
@skip true

@hideAll
@showUI MapUI

@skip false
@printer wait:true

@stop

; ==============================================================================
# Location1

@back Location1.RadialBlur
@choice  button:MapButton handler:ButtonArea

@if PlayerHasQuest==false
    @char Ayla.Idle pose:Center transition:DropFade time:0.3

    Ayla: Ну наконец-то, {PlayerName}. Мне нужна твоя помощь.
    Player: Рассказывай.
    Ayla: До меня дошли слухи, что где-то в пустоши находится одна очень интересная вещица. И мы с тобой могли бы неплохо наэтом заработать.
    Player: И где мне её искать?
    Ayla: В городе есть один человек, Дрейвен. Он обладает нужной информацией. Но он не отдаст её просто так.

    @set PlayerHasQuest=true
    @showUI QuestlogUI
    @questlog message:"Найти Дрейвена."

    Player: Ладно, посмотрю, что можно сделать.

    @hidePrinter
    @hideChars
    @goto .Map

@else
    @if PlayerHasItem==true
        @goto .FinalDialog
    @else
        @showUI QuestlogUI
        @char Ayla.Idle pose:Center transition:DropFade time:0.3
        Ayla: Не теряй время.
        @goto .Map
    @endif
@endif


; ==============================================================================
# Location2

@back Location2.RadialBlur
@choice  button:MapButton handler:ButtonArea

@if PlayerHasQuest==false
    @showUI QuestlogUI
@endif

@if PlayerHasItem==false
    @char Draven.Idle pose:Right transition:DropFade time:0.3

    @if Location3Unlocked==false

        @if MinigameCompleted==false

            @if MinigameAccepted==false
                Draven: Привет. Очередной посланник Айлы?
                Draven: Как звать то?
                Player: {PlayerName}
                Draven: Ну что ж, {PlayerName}, я могу тебе помочь, но сначала ты должен пройти мою проверку.

                @set MinigameAccepted=true
                @questlog message:"Пройти проверку Дрейвена."
            @else
                Draven: Продолжим?
            @endif

            Player: Валяй.

            @hideUI QuestlogUI
            @hidePrinter
            @hideChars

            ; @minigame

            @set MinigameCompleted=true
            @showUI QuestlogUI
            @char Draven.Idle pose:Right transition:DropFade time:0.3

            Draven: Хмм. Неплохо. На что-то да сгодишься.
            Player: Теперь твой черёд.
        @endif

        Draven: Вот тебе координаты заброшенного хранилища. Там ты найдешь то, что ищешь.
        Draven: Но учти, место это очень опасное.
        Draven: И да... Я жду свою долю. Не забудь обо мне на обратном пути.

        @set Location3Unlocked=true
        @questlog message:"Отправиться в хранилище."
        Player: Ясно.

        @goto .Map

    @else
            Draven: Ты уже получил, что хотел.
            @goto .Map
    @endif

@else
    @back Location2.RadialBlur

    Player: Хмм... Никого нет.
    Player: Ну тогда сразу к Айле.

    @hidePrinter
    @goto .Map
@endif


; ==============================================================================
# Location3

@back Location3.RadialBlur
@choice button:MapButton handler:ButtonArea wait:false
@if PlayerHasQuest==false
    @showUI QuestlogUI
@endif

@if PlayerHasItem==false
    @if ItemFound==false
        @questlog message:"Найти загадочный предмет."
        Player: Тааакс... поищем.
    @endif

    @choice  button:Item pos:0,-20 handler:ButtonArea goto:.ItemReceived

    @if ItemFound==false
        Player: А вот, кажется, и она.
        @set ItemFound=true
    @else
        Player: Нужно забирать скорее.
    @endif

    @stop

    # ItemReceived
    @set PlayerHasItem=true
    @set Location3Unlocked=false

    @questlog message:"Предмет получен."
    @questlog message:"Вернуться к Дрейвену."

    Player: Виглядит и правда очень интерсно.

    @hidePrinter
    @goto .Location2

@else
    Player: Я уже всё нашёл, нужно возвращаться.
    @goto .Map
@endif


; ==============================================================================
# FinalDialog

@char Ayla.Idle pose:Center transition:DropFade time:0.3
Ayla: Эта вещица у тебя? Давай её сюда скорее.

@char Draven.Idle pose:Right transition:DropFade time:0.3
Draven: Я не сомневался, что ты справишься, {PlayerName}. Ты со мной?

@choice "Отдать предмет Айле" goto:.AylaChoice
@choice "Отдать предмет Дрейвену" goto:.DravenChoice
@choice "Оставить предмет себе" goto:.KeepItem
@stop

# AylaChoice
    Player: Айла, держи.
    Ayla: Мы разбогатеем, {PlayerName}. Я уже чувствую это.
    @shake Draven
    Draven: Зря ты так!
    @goto .End

# DravenChoice
    Player: Дрейвен, держи.
    Draven: Отлично, {PlayerName}. Ты не пожалеешь о своём выборе.
    @shake Ayla
    Айла: Ты что, с ума сошел? Это же моя находка!
    @goto .End

# KeepItem
    Player: Пожалуй, я оставлю себе.
    @shake Ayla
    Ayla: Что? У нас был уговор!
    @shake Draven
    Draven: Зря ты! Я такое на прощаю!
    @goto .End

# End
    @questlog message:"Квест завершен."

    Player: Ну я погнал! Приятно иметь с вами дело!

    @hideUI QuestlogUI
    @hide Dialogue
    @hide Draven
    @hide Ayla
    @title

@stop
