@input PlayerName summary:"Как звать твоего героя?"
@stop

@set AylaName="Айла"
@set DravenName="Дрейвен"
@set PlayerHasItem=false

; ==============================================================================
# Location1

@back Location1.RadialBlur

@if PlayerHasItem==false
    @char Ayla.Idle pose:Center transition:DropFade time:0.3

    Ayla: Ну наконец-то, {PlayerName}. Мне нужна твоя помощь.
    Player: Рассказывай.
    Ayla: До меня дошли слухи, что где-то в пустоши находится одна очень интересная вещица. И мы с тобой могли бы неплохо на этом заработать.
    Player: И где мне её искать?
    Ayla: В городе есть один человек, Дрейвен. Он обладает нужной информацией. Но он не отдаст её просто так.

    @showUI QuestlogUI
    @questlog message:"Найти Дрейвена."

    Player: Ладно, посмотрю, что можно сделать.

    @hide Dialogue
    @hide Ayla

@else
    @goto .FinalDialog
@endif


; ==============================================================================
# Location2

@back Location2.RadialBlur

@if PlayerHasItem==false
    @char Draven.Idle pose:Right transition:DropFade time:0.3

    Draven: Привет. Очередной посланник Айлы?
    Draven: Как звать то?
    Player: {PlayerName}
    Draven: Ну что ж, {PlayerName}, я могу тебе помочь, но сначала ты должен пройти мою проверку.
    @questlog message:"Пройти проверку Дрейвена."

    Player: Валяй.

    @hideUI QuestlogUI
    @hide Dialogue
    @hide Draven

    ; @minigame

    @showUI QuestlogUI
    @char Draven.Idle pose:Right transition:DropFade time:0.3

    Draven: Хмм. Неплохо. На что-то да сгодишься.
    Player: Теперь твой черёд.
    Draven: Да, конечно. Вот тебе координаты заброшенного хранилища. Там ты найдешь то, что ищешь.
    Draven: Но учти, место это очень опасное.
    Player: Ясно.

    @questlog message:"Отправиться в хранилище."

    Draven: И да... Я жду свою долю. Не забудь обо мне на обратном пути.

    @hide Dialogue
    @hide Draven

    @goto .Location3

@else
    @back Location2.RadialBlur

    Player: Хмм... Никого нет.
    Player: Ну тогда сразу к Айле.

    @hide Dialogue

    @goto .Location1
@endif


; ==============================================================================
# Location3

@hide Dialogue
@hide Draven
@hide Ayla

@back Location3.RadialBlur

@if PlayerHasItem==false
    @questlog message:"Найти загадочный предмет."

    Player: Тааакс... поищем.

    @choice handler:ButtonArea button:Item pos:0,-20 goto:.ItemReceived

    Player: А вот, кажется, и она.
    @stop

    # ItemReceived
    @set PlayerHasItem=true
    @questlog message:"Предмет получен."
    @questlog message:"Вернуться к Дрейвену."

    Player: Виглядит и правда очень интерсно.

    ; TODO: deactivate Location 3
    
    @hide Dialogue
@endif

@goto .Location2

; ==============================================================================
# FinalDialog

@char Ayla.Idle pose:Center transition:DropFade time:0.3
Ayla: Эта вещица у тебя? Давай её сюда скорее.

@char Draven.Idle pose:Right transition:DropFade time:0.3
Draven: Я не сомневался, что ты справишься, {PlayerName}. Ты со мной?

@choice "Отдать предмет Айле" goto:.AylaChoice
@choice "Отдать предмет Дрейвену" goto:.DravenChoice
@choice "Оставить предмет себе" goto:.KeepItem
@stop

# AylaChoice
    Player: Айла, держи.
    Ayla: Мы разбогатеем, {PlayerName}. Я уже чувствую это.
    @shake Draven
    Draven: Зря ты так!
    @goto .End

# DravenChoice
    Player: Дрейвен, держи.
    Draven: Отлично, {PlayerName}. Ты не пожалеешь о своём выборе.
    @shake Ayla
    Айла: Ты что, с ума сошел? Это же моя находка!
    @goto .End

# KeepItem
    Player: Пожалуй, я оставлю себе.
    @shake Ayla
    Ayla: Что? У нас был уговор!
    @shake Draven
    Draven: Зря ты! Я такое на прощаю!
    @goto .End

# End
@questlog message:"Квест завершен."

Player: Ну я погнал! Приятно иметь с вами дело!

@hideUI QuestlogUI
@hide Dialogue
@hide Draven
@hide Ayla
@title

@stop
