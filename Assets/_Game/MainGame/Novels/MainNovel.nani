@back SceneBg
@input PlayerName summary:"What's your hero's name?"
@stop

@set AylaName="Ayla"
@set DravenName="Draven"

@set PlayerHasQuest=false
@set PlayerHasItem=false
@set Location3Unlocked=false
@set MinigameAccepted=false
@set MinigameCompleted=false
@set ItemFound=false
@set IsRaining=false

; ==============================================================================
# Map

@printer wait:false
@skip true

@hideAll

@if IsRaining==true
    @rain power:0 time:0.2
    @set IsRaining=false
@endif

@showUI MapUI

@skip false
@printer wait:true

@stop

; ==============================================================================
# Location1

@if IsRaining==true
    @rain power:0 time:0.2
    @set IsRaining=false
@endif

@back Location1.RadialBlur
@choice  button:MapButton handler:ButtonArea

@if PlayerHasQuest==false
    @char Ayla.Idle pose:Center transition:DropFade time:0.3

    Ayla: Finally, {PlayerName}. I need your help.
    Player: I'm listening.
    Ayla: I've heard rumors that there's something very interesting out in the wasteland. You and I could make a good profit from it.
    Player: And where should I look for it?
    Ayla: There's a man in town named Draven. He has the information we need. But he won't give it away for free.

    @set PlayerHasQuest=true
    @showUI QuestlogUI
    @questlog message:"Find Draven."

    Player: Alright, I'll see what I can do.

    @hidePrinter
    @hideChars
    @goto .Map

@else
    @if PlayerHasItem==true
        @goto .FinalDialog
    @else
        @showUI QuestlogUI
        @char Ayla.Idle pose:Center transition:DropFade time:0.3
        Ayla: Don’t waste time.
        @goto .Map
    @endif
@endif


; ==============================================================================
# Location2

@if IsRaining==false
    @rain wait:false
    @set IsRaining=true
@endif

@back Location2.RadialBlur

@choice  button:MapButton handler:ButtonArea

@if PlayerHasQuest==false
    @showUI QuestlogUI
@endif

@if PlayerHasItem==false
    @char Draven.Idle pose:Right transition:DropFade time:0.3

    @if Location3Unlocked==false

        @if MinigameCompleted==false

            @if MinigameAccepted==false
                Draven: Hey! Another one of Ayla's errand boys?
                Draven: What's your name?
                Player: You can call me {PlayerName}.
                Draven:  Well then, {PlayerName}, I can help you — but first, you'll have to pass my test.

                @set MinigameAccepted=true
                @questlog message:"Pass Draven's test."
            @else
                Draven: Shall we continue?
            @endif

            Player: Go ahead.

            @hideUI QuestlogUI
            @hidePrinter
            @hideChars

            @minigame

            @set MinigameCompleted=true
            @showUI QuestlogUI
            @char Draven.Idle pose:Right transition:DropFade time:0.3

            Draven: Hmm. Not bad. You might be useful after all.
            Player:  Now it's your turn.
        @endif

        Draven: Here are the coordinates to the abandoned vault. You'll find what you're looking for there.
        Draven: But keep in mind — the place is dangerous.
        Draven: And yeah... I expect my cut. Don't forget me on your way back.

        @set Location3Unlocked=true
        @questlog message:"Head to the vault."
        Player: Got it.

        @if IsRaining==true
            @rain power:0 time:0.2
            @set IsRaining=false
        @endif

        @goto .Map

    @else
            Draven: You already got what you came for.

            @if IsRaining==true
                @rain power:0 time:0.2
                @set IsRaining=false
            @endif

            @goto .Map
    @endif

@else
    Player: Hmm... No one's here.
    Player: Then straight back to Ayla.

    @hidePrinter

    @if IsRaining==true
        @rain power:0 time:0.2
        @set IsRaining=false
    @endif

    @goto .Map
@endif


; ==============================================================================
# Location3

@if IsRaining==true
    @rain power:0 time:0.2
    @set IsRaining=false
@endif

@back Location3.RadialBlur
@choice button:MapButton handler:ButtonArea wait:false
@if PlayerHasQuest==false
    @showUI QuestlogUI
@endif

@if PlayerHasItem==false
    @if ItemFound==false
        @questlog message:"Find the mysterious thing."
        Player: Alright... let’s look around.
    @endif

    @choice  button:Item pos:0,-20 handler:ButtonArea goto:.ItemReceived

    @if ItemFound==false
        Player: There it is, I think.
        @set ItemFound=true
    @else
        Player: Better grab it quickly.
    @endif

    @stop

    # ItemReceived
    @set PlayerHasItem=true
    @set Location3Unlocked=false

    @questlog message:"Item acquired."
    @questlog message:"Return to Draven."

    Player: It really does look interesting.

    @hidePrinter
    @goto .Location2

@else
    Player: I’ve already found everything — time to head back.
    @goto .Map
@endif


; ==============================================================================
# FinalDialog

@char Ayla.Idle pose:Center transition:DropFade time:0.3
Ayla: You've got it? Give it to me.

@char Draven.Idle pose:Right transition:DropFade time:0.3
Draven: I knew you'd pull it off, {PlayerName}. Are you with me?

@choice "Give the item to Ayla" goto:.AylaChoice
@choice "Give the item to Draven" goto:.DravenChoice
@choice "Keep the item for yourself" goto:.KeepItem
@stop

# AylaChoice
    Player: Here you go, Ayla.
    Ayla: We're going to be rich, {PlayerName}. I can feel it already!
    @shake Draven
    Draven: You’ll regret that!
    @goto .End

# DravenChoice
    Player: Here you go, Draven.
    Draven: Excellent, {PlayerName}. You won’t regret your decision.
    @shake Ayla
    Айла: Are you crazy? That was *my* discovery!
    @goto .End

# KeepItem
    Player: I think I’ll keep it for myself.
    @shake Ayla
    Ayla: What? We had a deal!
    @shake Draven
    Draven: Big mistake! I don’t forgive things like that!
    @goto .End

# End
    @questlog message:"Quest complete."

    Player: Good luck! It was a pleasure doing business with you!

    @hideUI QuestlogUI
    @hide Dialogue
    @hide Draven
    @hide Ayla
    @title

@stop
